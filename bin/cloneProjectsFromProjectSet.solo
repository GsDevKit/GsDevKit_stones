#!/usr/bin/env superdoit_solo
options
{
SuperDoitOptionalOptionWithRequiredArg long: 'registry' short: 'r'.
SuperDoitOptionalOptionWithRequiredArg long: 'remote' default: 'origin'.
SuperDoitRequiredOptionWithRequiredArg long: 'projectSet'.
SuperDoitOptionalOptionWithRequiredArg long: 'projectDirectory'.
SuperDoitOptionalOptionWithNoArg long: 'update'.
}
%
usage
-----
USAGE $basename [--help | -h] [--debug | -D]  [--debugGem] \
                [--registry=<registry-name> | -r <registry-name>] \
                --projectSet=<projectSet-name> [--projectDirectory=<project-directory>]

DESCRIPTION
  A project set is a list of load specs  that specify a group of git repositories that are managed together. 

CLONE git commands for each project
  git clone <project-url>
  git checkout -B remote/revision
  git remote add remote <additional-project-project-urls>
  git fetch --all

UPDATE git commands for each project
  git checkout remote/revision
  git pull remote revision


OPTIONS
  -h, --help        display usage message
  -D, --debug       bring up topaz debugger in the event of a script error
  --debugGem        If terminal is connected to stdout, bring up debugger. If not,
                    dump stack to stdout and wait for topaz to attach using topaz
                    DEBUGGEM command.
  --registry=<registry-name> | -r <registry-name> 
                    <registry-name> is the name of the registry. Default is hostname.
  --projectSet=<projectSet-name>
                    Name of the project set to use.
  --projectDirectory=<project-directory>
                    Path to directory where the projects will be cloned. If not
                    specified the projectDirectory associated with the project set 
                    is used.
  --update          If present, a pull request will be issued for the origin remote
                    to update the repository to the latest version of the branch 
                    available.
  <project-name>...
                    List of project names in project to be cloned. If not specified
                    all of the projects in the project set will be cloned.

EXAMPLES
  $basename --help
  $basename -D
  $basename --debugGem
  $basename 
  $basename --registry=bosch --projectSet=dev --remote=https Rowan \
            --projectDirectory=/home/dhenrich/bosch1/_exp_git
  $basename -r=bosch --projectSet=_stones Rowan --update
-----
%
specs
[
RwLoadSpecificationV2 {
	#projectName : 'GsDevKit_stones',
	#projectSpecFile : 'rowan/project.ston',
	#componentNames : [
		'Core',
		'Solo'
	],
	#platformProperties : {
		'gemstone' : {
			'allusers' : {
				#defaultSymbolDictName : 'Globals'
			}
		}
	},
	#comment : ''
}
]
%
method
registryName
	^ self registry
		ifNil: [ (self globalNamed: 'System') hostname ]
		ifNotNil: [:value | value ].
%
doit
	| registryClass stonesRegistry name projectSet projectSetPath projectDir |
	self preDoitSpecLoad: [:spec |
		spec projectsHome: self dirname asFileReference parent parent].

	registryClass := (self globalNamed: 'GDKRegistry').	
	stonesRegistry := registryClass stonesRegistryNamed: self registryName.
	name := self projectSet.
	projectSetPath := stonesRegistry projectSets 
		at: name 
		ifAbsent: [ self error: 'Cannot find project set named ', name printString, ' (', self basename, ')' ].
	projectSet := (self globalNamed: 'GDKProjectSet') 
				fromPath: projectSetPath 
				ifAbsent: [ self error: 'Cannot find project set ', projectSetPath printString, ' (', self basename, ')' ].
	projectDir := self projectDirectory
		ifNotNil: [:dir | 
			projectSet projectDirectory: dir.
			projectSet export.
			dir asFileReference fullName ]
		ifNil: [ 
			projectSet projectDirectory
				ifNil: [ 
					stonesRegistry projectDirectory
						ifNil: [ self error: '--projectDirectory option must be used if no project directory defined in project set', ' (', self basename, ')' ] ] ].
	projectDir asFileReference ensureCreateDirectory. 
	Rowan projectTools trace startTracing.
	self positionalArgs size = 0
		ifTrue: [ projectSet cloneOrUpdateProjectSet: projectDir remoteName: self remote pull: self update]
		ifFalse: [ 
			self positionalArgs  do: [:projectName |
				projectSet cloneOrUpdateProject: projectName remoteName: self remote in: projectDir pull: self update] ].
	true
%
