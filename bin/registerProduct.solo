#!/usr/bin/env superdoit_solo
options
{
SuperDoitOptionalOptionWithNoArg long: 'ensure' short: 'e'.
SuperDoitOptionalOptionWithNoArg long: 'force' short: 'f'.
SuperDoitOptionalOptionWithRequiredArg long: 'registry' short: 'r'.
SuperDoitOptionalOptionWithRequiredArg long: 'productPath'.
SuperDoitOptionalOptionWithRequiredArg long: 'fromDirectory'.
}
%
Usage
-----
USAGE $basename [--help | -h] [--debug | -D]  [--debugGem] \
                [--registry=<registry-name> | -r <registry-name>] \
                [ [ --productPath=<path-to-product-tree> <gemstone-version> ] \
                  | \
                  [ --fromDirectory=<path-to-directory-of-productTrees> ] ]

DESCRIPTION
  Register a GemStone product tree with <registry-name> that is already on disk.

OPTIONS
  -h, --help      display usage message
  -D, --debug     bring up topaz debugger in the event of a script error
  --debugGem      If terminal is connected to stdout, bring up debugger. If not,
                  dump stack to stdout and wait for topaz to attach using topaz
                  DEBUGGEM command.
  --registry=<registry-name> | -r <registry-name> 
                  <registry-name> is the name of the registry. Default is hostname.
  --productPath=<source-product-directory>
                  A directory reference to <source-product-directory> will be stored
                  in the registry, using <gemstone-version> as the key.
  --fromDirectory=<path-to-directory-of-productTrees>
                  Directory where the subdirectories are GemStone product trees.
                  A reference to each of the product trees in the directory will be
                  added to <registry-name>.
  --force         Override existing entries with new product path.
  --ensure        If an entry already exists, so not override

EXAMPLES
  $basename --help
  $basename -D
  $basename --registry=bosch --productPath=/export/bosch1/users/dhenrich/_work/c_37x/fast50/gs/product 3.7.0 
  $basename -r bosch --fromDirectory=$GS_HOME/shared/downloads/products 
-----
%
specs
[
RwLoadSpecificationV2 {
	#projectName : 'GsDevKit_stones',
	#projectSpecFile : 'rowan/project.ston',
	#componentNames : [
		'Core',
		'Solo'
	],
	#platformProperties : {
		'gemstone' : {
			'allusers' : {
				#defaultSymbolDictName : 'Globals'
			}
		}
	},
	#comment : ''
}
]
%
method
registryName
	^ self registry
		ifNil: [ (self globalNamed: 'System') hostname ]
		ifNotNil: [:value | value ].
%
doit
	| registryClass stonesRegistry gemstoneVersion gemstonePath |
	self preDoitSpecLoad: [:spec |
		spec projectsHome: self dirname asFileReference parent parent].
	registryClass := (self globalNamed: 'GDKRegistry').	
	stonesRegistry := registryClass stonesRegistryNamed: self registryName.
	self productPath
		ifNotNil: [:productPath |
			gemstonePath := GsFile serverRealPath: self productPath.
			self positionalArgs size = 1
				ifTrue: [ gemstoneVersion := self positionalArgs at: 1 ] 
				ifFalse: [ self error: 'Expected a single positional argument for GemStone version' ].
			(stonesRegistry products at: gemstoneVersion ifAbsent: [])
				ifNotNil: [:existingPath | 
					self ensure
						ifTrue: [ 
							"ignore incoming path, since all we want to do is make sure that it is defined"
							gemstonePath := existingPath ]
						ifFalse: [ 
							self force 
								ifFalse: [ 
									self error: 'There is an existing path (', 
										existingPath,
										') for the GemStone version ', 
										gemstoneVersion, 
										'. Use the --force option to override or --ensure option to use existing path.', ' (', self basename, ')' ] ] ].
			stonesRegistry products at: gemstoneVersion put: gemstonePath ]
		ifNil: [
			self fromDirectory
				ifNotNil: [:directory |
					(directory asFileReference children select: [:entity | entity isDirectory ])
						do: [:dir | 
							| productPrefix dirName |
							productPrefix := 'GemStone64Bit3'.
							dirName := dir basename.
							(dirName beginsWith: productPrefix)
								ifTrue: [
									| gemstoneVersion dashIndex |
									dashIndex := dirName findLastSubString: '-' startingAt: dirName size.
									(dashIndex notNil and: [ dashIndex > 0 ])
										ifTrue: [
											gemstoneVersion := dirName copyFrom: productPrefix size to: dashIndex - 1.
											(stonesRegistry products at: gemstoneVersion ifAbsent: [])
												ifNotNil: [
													self force
														ifTrue: [ stonesRegistry products at: gemstoneVersion put: dir fullName ] ]
												ifNil: [ stonesRegistry products at: gemstoneVersion put: dir fullName ] ] ] ] ]
				ifNil: [ self error: 'one of --productPath or --fromDirectory is required', ' (', self basename, ')' ] ].
	stonesRegistry export.

%
