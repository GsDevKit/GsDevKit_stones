#!/usr/bin/env superdoit_solo
options
{
SuperDoitRequiredOptionWithRequiredArg long: 'registry'.
SuperDoitRequiredOptionWithRequiredArg long: 'projectSet'.
SuperDoitOptionalOptionWithRequiredArg long: 'from'.
SuperDoitOptionalOptionWithRequiredArg long: 'key'.
SuperDoitOptionalOptionWithNoArg long: 'ssh'.
SuperDoitOptionalOptionWithNoArg long: 'https'.
SuperDoitOptionalOptionWithNoArg long: 'spec'.
}
%
usage
-----
USAGE $basename [--help | -h] [--debug | -D]  [--debugGem] \
                registry=<registry-name> --projectSet=<projectSet-name>
      $basename registry=<registry-name> --projectSet=<projectSet-name> \
                --from=gsdevkitProjectSpecs.ston --key=server --ssh
      $basename registry=<registry-name> --projectSet=<projectSet-name> \
                --from=gsdevkitProjectSpecs.ston --key=clientServer --https
      $basename registry=<registry-name> --projectSet=<projectSet-name> \
                --from=gsdevkitProjectSpecs.ston --key=clientServer --spec

DESCRIPTION
  A project set is a list of load specs that specify a group of git repositories that are managed together. 

OPTIONS
  -h, --help        display usage message
  -D, --debug       bring up topaz debugger in the event of a script error
  --debugGem        If terminal is connected to stdout, bring up debugger. If not,
                    dump stack to stdout and wait for topaz to attach using topaz
                    DEBUGGEM command.
  --registry=<registry-name>   
                    name of the registry. Default is /usr/bin/hostname
  --projectSet=<projectSet-name>
                    Name of the project set.

EXAMPLES
  $basename --help
  $basename -D
  $basename --debugGem
  $basename 
  $basename --registry=bosch --projectSet=shared
  $basename --registry=bosch --projectSet=gsdevkit_home --from=gsdevkitProjectSpecs.ston \
            --key=server --ssh 
  $basename --registry=bosch --projectSet=gsdevkit_home --from=gsdevkitProjectSpecs.ston \
            --key=clientServer --http
-----
%
specs
[
RwLoadSpecificationV2 {
	#projectName : 'GsDevKit_stones',
	#projectSpecFile : 'rowan/project.ston',
	#componentNames : [
		'Core',
		'Solo'
	],
	#platformProperties : {
		'gemstone' : {
			'allusers' : {
				#defaultSymbolDictName : 'Globals'
			}
		}
	},
	#comment : ''
}
]
%
method
constructGitUrlForProject: project spec: organizationArray
	"
		'https://github.com/dalehenrich/filetree.git' 
		'git@github.com:dalehenrich/filetree.git'
		'git@git.gemtalksystems.com:filetree.git'
	" 
	| site organization |
	site := organizationArray at: 1.
	organization := organizationArray size = 2
		ifTrue: [ (organizationArray at: 2), '/' ]
		ifFalse: [ 
			organizationArray size = 1
				ifFalse: [ self error: 'organization array (', organizationArray printString, ' must be of size one or two' ].
			'/' ].
	self https
		ifTrue: [ ^ 'https://', site, '/', organization, project, '.git' ]
		ifFalse: [
			self ssh
				ifTrue: [ ^ 'git@', site, ':', organization,  project, '.git']
				ifFalse: [ self error: 'Either --https or --ssh must be specified.'] ].
%
method
populateProjectSet: projectSet from: stonSpecDictionaryFilePath
	| specDictionary loadSpecs type |
	specDictionary := stonSpecDictionaryFilePath asFileReference 
		readStreamDo: [:fileStream | 
			(STON reader on:  (ZnBufferedReadStream on:  fileStream)) next ].
	loadSpecs := projectSet loadSpecs.
	(type := specDictionary at: 'type') = 'gitUrlSpec'
		ifTrue: [ 
			(specDictionary at: self key)
				keysAndValuesDo: [:projectName :projectDict |
					"
						'metacello-work' : {
							'revision' : 'master',
							'gitUrls' : {
								'origin' : [
								'github.com',
								'dalehenrich'
								],
								'gs' : [
								'git.gemtalksystems.com',
								''
								]
							}
						}
					"
					| specDict |
					specDict := Dictionary new
						at: 'revision' put: (projectDict at: 'revision');
						yourself.
					(projectDict at: 'gitUrls')
						keysAndValuesDo: [:origin :specArray |
							| gitUrl  |
							gitUrl := self constructGitUrlForProject: projectName spec: specArray.
							(specDict at: 'gitUrls' ifAbsentPut: [ Dictionary new ])
								at: origin put: gitUrl ].
					loadSpecs 
						at: projectName
						put: specDict ] ]
	ifFalse: [self error: 'The dictionary of type ', type printString , ' is not supported.'].
%
doit
	| registryClass stonesRegistry name projectSet |
	self preDoitSpecLoad: [:spec |
		spec projectsHome: self dirname asFileReference parent parent].
	registryClass := (self globalNamed: 'GDKRegistry').	
	stonesRegistry := registryClass stonesRegistryNamed: self registry.
	name := self projectSet.
	projectSet := ((self globalNamed: 'GDKProjectSet') newNamed: name)
		parentRegistryName: self registry;
		parentRegistryPath: (self globalNamed: 'GDKGsDevKit_stonesBase') dataHomeEnvVar, '/' , (stonesRegistry registryFile relativeTo: (self globalNamed: 'GDK_XDGBase') data_home) pathString;
		yourself.
	projectSet registryFile exists
		ifTrue: [
			| projectSetPath |
			projectSetPath := stonesRegistry projectSets at: name.
			projectSet := (self globalNamed: 'GDKProjectSet') 
				fromPath: projectSetPath
				ifAbsent: [ self error: 'Cannot find project set ', projectSetPath printString ] ]
		ifFalse: [	
			projectSet registryDir ensureCreateDirectory.
			projectSet export.
			stonesRegistry projectSets at: name put: projectSet registryPath.
			stonesRegistry export ].
  self from
		ifNotNil: [:stonSpecDictionaryFilePath | 
			self populateProjectSet: projectSet from: stonSpecDictionaryFilePath.
			projectSet export ].
	^ projectSet
%
