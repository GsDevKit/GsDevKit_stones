#!/usr/bin/env superdoit_solo
options
{
SuperDoitOptionalOptionWithRequiredArg long: 'directory'.
SuperDoitOptionalOptionWithRequiredArg long: 'registry'.
}
%
Usage
-----
USAGE $basename [--help | -h] [--debug | -D]  [--debugGem] \
      [--directory=<products-directory> | --registry=<registry-name>] \
      <gemstone-version> [ <server-dir-name> ]

DESCRIPTION
  Download and unzip the product tree for the given <gemstone-version> to the
  given <products-directory> or productDirectory defined by the <registry-name>.

OPTIONS
  -h, --help    display usage message
  -D, --debug   bring up topaz debugger in the event of a script error
  --debugGem    If terminal is connected to stdout, bring up debugger. If not,
                dump stack to stdout and wait for topaz to attach using topaz
                DEBUGGEM command.
  -f, --force   Delete the stone directory if it exists.
  -v, --verbose Verbose logging enabled.
  --directory=<products-directory>
                The directory where the product tree will be downloaded to.
  --registry=<registry-name>
                The product will be downloaded to the product 
                directory defined by the <registry-name> and it's location will
                be registered.

EXAMPLES
  $basename --help
  $basename -D
  $basename --debugGem
  $basename --registry=bosch 3.6.4
  $basename --registry=bosch 3.7.0 3.7.0_rowanv3-Alpha1
  $basename --directory=$GS_HOME/shared/downloads/products 3.6.4 
_rowanv3-Alpha1
-----
%
specs
[
RwLoadSpecificationV2 {
	#projectName : 'GsDevKit_stones',
	#projectSpecFile : 'rowan/project.ston',
	#componentNames : [
		'Core',
		'Solo'
	],
	#platformProperties : {
		'gemstone' : {
			'allusers' : {
				#defaultSymbolDictName : 'Globals'
			}
		}
	},
	#comment : ''
}
]
%
method
downloadToRegistry: registryName version: version serverDirName: serverDirName
	| registryClass stonesRegistry |
	registryClass := (self globalNamed: 'GDKRegistry').	
	stonesRegistry := registryClass stonesRegistryNamed: registryName.
	version = serverDirName
		ifTrue: [ stonesRegistry productNamed: version ]
		ifFalse: [ 
			stonesRegistry
				productNamed: serverDirName
				ifAbsent: [ 
					| productPath |
					productPath := self 
						downloadToDirectory: stonesRegistry productDirectory 
						version: version 
						serverDirName: serverDirName.
					stonesRegistry products at: serverDirName put: productPath ].
					stonesRegistry export ].
	^	self noResult
%
method
downloadToDirectory: directoryPath version: version serverDirName: serverDirName
	| registryClass |
	registryClass := self globalNamed: 'GDKStonesRegistry'.
	^ registryClass
		downloadProductName: serverDirName
		serverName: (registryClass downloadDirectoryNameFor:serverDirName)
		productDirectory: (registryClass downloadDirectoryNameFor: version)
		to: directoryPath.	
%
doit
	| version serverDirName |
	self preDoitSpecLoad: [:spec | 
		spec projectsHome: self dirname asFileReference parent parent ].
	self positionalArgs size < 1
		ifTrue: [ self error: 'Missing required positional argument for GemStone version' ]
		ifFalse: [ 
			self positionalArgs size = 1
				ifTrue: [ 
					version := self positionalArgs at: 1.
					serverDirName := version ]
				ifFalse: [ 
					self positionalArgs size = 2
						ifTrue: [ 
							version := self positionalArgs at: 1.
							serverDirName := self positionalArgs at: 2 ] 
						ifFalse: [ self error: 'Only 1 or 2 positional arguments are allowed' ] ] ].
	^ self registry
		ifNotNil: [:registryName |
			self downloadToRegistry: registryName version: version serverDirName: serverDirName ]
		ifNil: [
			self directory
				ifNil: [ self error: 'either --directoryName or --registryName must be specified' ]
				ifNotNil: [:directoryPath |
					self downloadToDirectory: directoryPath version: version serverDirName: serverDirName ] ].

%
